F<x>:=PolynomialRing(FiniteField(3));
G:=CSp(4,3);
ZG:=Subgroups(G);
S:=ZG[#ZG - 1]`subgroup;
C:=Classes(G);
P,phi:=quo<G|Center(G)>;
XG:=CharacterTable(G);

/* the element (-1,-1,-1,-1) */
c:=Center(G).1;
P,pi:=quo<G|sub<G|c>>;

/* the function isregular determines if g is a regular elment or not */

isregular := function(P)
Q:=Factorization(P);
count:=0;
for m in [1..#Q] do
	if Q[m][2] ge 2 then
		count:=1;
		end if;
	end for;
return 1-count;
end function;

print("list the conjugacy classes and orders of regular semisimple elements in GSp4(F3) up to conjugation in GSp_4(F3); first come the classes and the orders (can ignore), then explicit representatives");

print("the conjugacy classes of GSp4(F3), with [v(g),label,order(g),order(g in PSGp_4(F3))]");
for n in [1..#C] do
g:=C[n][3];
if g in S then
	[1,n,Order(g),Order(pi(g))];
	end if;
if g notin S then
	[-1,n,Order(g),Order(pi(g))];
	end if;
end for;

print("*********");
print("the subset of the above which are regular semisimple");
regularsemisimple:=[];
regularsemisimpleG:=[];
for n in [1..#C] do
g:=C[n][3];
P:=F!CharacteristicPolynomial(g);
if g in S then
	if GCD(Order(g),3) eq 1 then
		if isregular(P) eq 1 then
			[1,n,Order(g)];
			regularsemisimple:=Append(regularsemisimple,g);	
			end if;
		end if;
	end if;
if g notin S then
	if GCD(Order(g),3) eq 1 then
		if isregular(P) eq 1 then
			[-1,n,Order(g)];
			regularsemisimpleG:=Append(regularsemisimpleG,g);	
			end if;
		end if;
	end if;
end for;

print("the classes and orders in Sp4");
for g in regularsemisimple do
	g;
	print("with order");
	Order(g);
end for;

print("the classes and orders in GSp4 minus Sp4");
for g in regularsemisimpleG do
        g;
        print("with order");
        Order(g);
end for;




/* The following counts the number of elements in a group Gamma 
which are tidy. The only allowable possibility is that v(g) = -1. 
If 1 is an eigenvalue then so is -1, and g is not tidy. If -1
eigenvalue then so is 1, so need only rule out 1 from {1,-1}. Suppose 
the eigenvalues are of the form (a,-1/a,b,-1/b). Suppose that a ratio = - 1. 
The ratio of a/(-1/a) = -a^2 can't be -1 unless a=+-1. 
Hence we must have WLOG b = -a, and eigenvalues = (a,-1/a,-a,1/a), 
and the characteristic polynomial of g = char poly g*c for c = (-1,-1,-1,-1) */


function tidycount(Gamma);
counter:=0;
G:=Gamma meet S;
for g in Gamma do
	if g notin G then
		P:=F!CharacteristicPolynomial(g);
		Q:=F!CharacteristicPolynomial(g * c);
		if Evaluate(P,1) ne 0 then
			if P ne Q then
				counter:=counter+1;
			end if;
		end if;
	end if;
end for;
if counter ge 1 then
	counter:=1;
	end if;
return counter;
end function;

/* ARS is the list of regular semisimple elements in Sp_4(F_3)  */

ARS:=[];
for m in [1..3] do
	T:=Conjugates(G,regularsemisimple[m]);
	for n in [1..#T] do
		ARS:=Append(ARS,T[n]);
		end for;
	end for;

/* BARS is the list of regular semisimple elements in GSp_4(F_3) \ Sp_4(F_3)  */

BARS:=[];
for m in [1..5] do
	T:=Conjugates(G,regularsemisimpleG[m]);
	for n in [1..#T] do
		BARS:=Append(BARS,T[n]);
		end for;
	end for;


/* the list ARS should be the union of three conjugacy classes so their orders
and thus the next two numbers should coincide */

C[12][2] + C[24][2] + C[28][2];
#ARS;
#BARS;

/* reproduce Whitmore's table but now with regular-semisimple elements and ignoring
H^1(Gamma,F_3) */

good:=0;

print("reproducing Whitmore's table plus more, 1 means is good");
print("[Gamma', Gamma, reasonable, tidy, regular semisimple in Sp4, regular semisimple in GSp4 minus Sp4]");

for n in [3..#ZG] do
P:=ZG[n]`subgroup;
M:=GModule(P);
N:=GModule(P meet S);
if IsAbsolutelyIrreducible(N) then
sp:=SymmetricSquare(N);
CM:=CohomologyModule(P meet S,sp);
if Index(P,P meet S) eq 2 then
	count:=0; countG:=0;
	for g in P do
		if count eq 0 then
			if g in ARS then
				count:=count+1;
				end if;
			end if;
		if countG eq 0 then
			if g in BARS then
				countG:=countG+1;
				end if;
			end if;
		end for;
/*  [n,Order(P),Order(P meet S),Dimension(CohomologyGroup(CM,1)),tidycount(P),count,countG]; */
	[Order(P),Order(P meet S),1-Dimension(CohomologyGroup(CM,1)),tidycount(P),count,countG];
	if ((1-Dimension(CohomologyGroup(CM,1)))*tidycount(P)*count*countG) eq 1 then
		good:=good+1;
		end if;
if Order(P) le 2000 then
IdentifyGroup(P);
	end if;
if Order(P meet S) le 1000 then
IdentifyGroup(P meet S);
end if;
	end if;
	end if;
end for;

print("***the number of groups satisfying all properties is***");
good;
